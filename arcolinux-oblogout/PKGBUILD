# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: XaBz <xabz.2k5@gmail.com>
# Edited by Erik Dubois for ArcoLinux

pkgname=arcolinux-oblogout
_pkgname=oblogout
pkgver=0.2
pkgrel=19
pkgdesc='Openbox logout script'
arch=('any')
url='https://launchpad.net/oblogout'
license=('GPL2')
depends=('pygtk' 'python2-pillow' 'python2-dbus')
makedepends=('python2-distutils-extra')
backup=('etc/oblogout.conf')
source=("https://launchpad.net/oblogout/$pkgver/$pkgver.0/+download/$_pkgname-$pkgver.tar.bz2"
        'oblogout.patch'
        "$_pkgname.conf"
        'ru.po')
sha256sums=('f80003b85e9f4f1c588675453ea03f40a86fc50546d509ceb69706eb045782a9'
            'cea52a14df0ef924fcdd63b2e82c14b94cc2a7753994c176621ace9502bd9109'
            'ced18597ccb69b930d0530656a11c114bd5b17ac38f2b9a8122daeee9f02c3b2'
            'aa8504055ec1e0661c3588cde85aad3267ffa7a42414c106e6ce8bff71f9d883')
prepare() {
  cd "$_pkgname"

  patch -p0 -i ../oblogout.patch
  sed -i 's/fromstring/frombytes/' oblogout/__init__.py # FS#46587
  sed -i '0,/on/s//on2/' "$_pkgname"/*.py
  cp "$srcdir/ru.po" po
}

package() {
  cd "$_pkgname"

  python2 setup.py install --root="$pkgdir"
  install -m644 "$srcdir/oblogout.conf" "$pkgdir/etc/"
}

