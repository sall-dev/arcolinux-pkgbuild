# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: XaBz <xabz.2k5@gmail.com>
# Edited by Erik Dubois for ArcoLinux
# Original version 0.2-19

pkgname=arcolinux-oblogout
_pkgname=oblogout
_destname="/usr/local/bin/"
pkgver=0.2
pkgrel=19
pkgdesc='Openbox logout script'
arch=('any')
url='https://launchpad.net/oblogout'
license=('GPL2')
depends=('pygtk' 'python2-pillow' 'python2-dbus')
makedepends=('python2-distutils-extra')
replaces=('arcolinux-oblogout-awesome'
          'arcolinux-oblogout-bspwm'
          'arcolinux-oblogout-herbstluftwm'
          'arcolinux-oblogout-qtile'
          'arcolinux-oblogout-xmonad')
backup=('etc/oblogout.conf')
conflicts=("oblogout")
source=("https://launchpad.net/oblogout/$pkgver/$pkgver.0/+download/$_pkgname-$pkgver.tar.bz2"
        'oblogout.patch'
        "$_pkgname.conf"
        'ru.po'
        'arco-oblogout')
sha256sums=('f80003b85e9f4f1c588675453ea03f40a86fc50546d509ceb69706eb045782a9'
            'cea52a14df0ef924fcdd63b2e82c14b94cc2a7753994c176621ace9502bd9109'
            '953b77929fc91229fd7f819c3626424471d7195bd07e00015ecb1730b48c7165'
            'aa8504055ec1e0661c3588cde85aad3267ffa7a42414c106e6ce8bff71f9d883'
            'a21a3f4cdf96cddd41707f1358b51890ed61c89d57d53b7e1e2408dd77c49b46')
prepare() {
  cd "$_pkgname"

  patch -p0 -i ../oblogout.patch
  sed -i 's/fromstring/frombytes/' oblogout/__init__.py # FS#46587
  sed -i '0,/on/s//on2/' "$_pkgname"/*.py
  cp "$srcdir/ru.po" po
}

package() {
  cd "$_pkgname"

  python2 setup.py install --root="$pkgdir"
  install -m644 "$srcdir/oblogout.conf" "$pkgdir/etc/"
  install -d -m755 "${pkgdir}${_destname}"
  install -m755 "${srcdir}/"arco-oblogout "${pkgdir}${_destname}arco-oblogout"
  #chown root:root ${pkgdir}${_destname}arco-oblogout
  #chmod 755 ${pkgdir}${_destname}arco-oblogout
}
