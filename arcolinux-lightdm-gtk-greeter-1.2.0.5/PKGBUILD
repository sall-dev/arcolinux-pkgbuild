# $Id$
# Maintainer: Maxime Gauduin <alucrydarchlinux.org>
# Contributor: Padfoot <padfoot@exemail.com.au>
# Source : https://launchpad.net/lightdm-gtk-greeter
# Edited by Erik Dubois for ArcoLinux

pkgname=arcolinux-lightdm-gtk-greeter
pkgver=2.0.5
pkgrel=1
pkgdesc='GTK+ greeter for LightDM'
arch=('x86_64')
url='https://github.com/arcolinux/arcolinux-lightdm-gtk-greeter'
license=('GPL3' 'LGPL3')
depends=('gtk3' 'lightdm')
makedepends=('exo' 'gobject-introspection' 'intltool')
replaces=('lightdm-gtk2-greeter' 'lightdm-gtk3-greeter')
backup=('etc/lightdm/arcolinux-lightdm-gtk-greeter.conf')
source=(${pkgname}::"git+https://github.com/arcolinux/${pkgname}.git")
sha256sums=('SKIP')

build() {
  cd arcolinux-lightdm-gtk-greeter

  export CFLAGS="$CFLAGS -Wno-declaration-after-statement"

  ./configure \
    --prefix='/usr' \
    --libexecdir='/usr/lib/lightdm' \
    --sbindir='/usr/bin' \
    --sysconfdir='/etc' \
    --with-libxklavier \
    --enable-kill-on-sigterm \
    --disable-libido \
    --disable-libindicator \
    --disable-static
  make
}

package() {
  cd arcolinux-lightdm-gtk-greeter

  make DESTDIR="${pkgdir}" install
}

